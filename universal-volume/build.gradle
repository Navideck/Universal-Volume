import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

android {
    namespace 'com.navideck.universal_volume'
    compileSdk 33

    defaultConfig {
        minSdk 16
        targetSdk 33
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}


//https://kotlinlang.org/docs/dokka-gradle.html#general-configuration
tasks.withType(DokkaTask.class) {
    outputDirectory.set(file("../docs"))
    moduleName.set("universal-volume")
    dokkaSourceSets.configureEach {
        displayName.set("UniversalVolume")
        perPackageOption {
            matchingRegex.set(".*volume_manager.*")
            suppress.set(true)
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'org.jetbrains.dokka:android-documentation-plugin:1.8.20'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.Navideck'
            artifactId = 'Universal-Volume'
            version = '1.1'
            afterEvaluate {
                from components.release
            }
        }
    }
}